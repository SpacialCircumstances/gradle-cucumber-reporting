plugins {
    id "groovy"
    id "com.gradle.plugin-publish" version "0.10.0"
    id "java-gradle-plugin"
    id 'maven-publish'
}

version = '0.1.18'
group = "com.github.spacialcircumstances"

repositories {
    jcenter()
}

dependencies {
    implementation group: 'net.masterthought', name: 'cucumber-reporting', version: '5.1.1'
    implementation localGroovy()
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

gradlePlugin {
    plugins {
        cucumberReportsPlugin {
            id = 'com.github.spacialcircumstances.gradle-cucumber-reporting'
            implementationClass = 'com.github.spacialcircumstances.gradle.ReportsPlugin'
        }
    }
}

publishing {
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/SpacialCircumstances/gradle-cucumber-reporting'
    vcsUrl = 'https://github.com/SpacialCircumstances/gradle-cucumber-reporting'
    description = 'A gradle plugin to generate pretty HTML reports for Cucumber from json report files. Uses https://github.com/damianszczepanik/cucumber-reporting for generating the reports.'

    plugins {
        cucumberReportsPlugin {
            id = 'com.github.spacialcircumstances.gradle-cucumber-reporting'
            displayName = 'Gradle cucumber reporting'
            tags = ['cucumber', 'reports', "test"]
        }
    }
}

sourceSets {
    functionalTest {
        groovy {
            srcDir file('src/functionalTest/groovy')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        testLogging.showStandardStreams = true
    }
    mustRunAfter test
}

test.finalizedBy functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}
